apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

ktlint {
    version = "$ktlint_version"
    android = true
    outputToConsole = true
    ignoreFailures = false
}

sqldelight {
    Database {
        packageName = "nl.tudelft.common.sqldelight"
        sourceFolders = ["sqldelight"]
        schemaOutputDirectory = file("src/main/sqldelight/databases")
    }
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField "String", "DEFAULT_GATEWAY_IP", "\"0.0.0.0\""
        buildConfigField "Integer", "DEFAULT_GATEWAY_PORT", "8090"
        buildConfigField "String", "DEFAULT_GATEWAY_PK", "\"4c69624e61434c504b3ac16efa9bc1784b76d418dc051fca4e4ac645fd68699a26dfec1d1e880846f45c184297a0b52109f67de0d49dbc52fea3328d1d73a58ff3b99bfda453cbdcd350\""
        buildConfigField "String", "DEFAULT_GATEWAY_NAME", "\"Localhost Gateway\""
        buildConfigField "String", "DEFAULT_GATEWAY_HOST", "\"http://127.0.0.1:8000\""


//        buildConfigField "String", "BITCOIN_FAUCET", "\"http://134.122.59.107:3000\""
        buildConfigField "String", "BITCOIN_FAUCET", "\"https://taproot.tribler.org\""

        // 10.0.2.2 - IP address of the host system on Android emulator
        // Refer to included Docker Compose to launch the nodes locally
//        buildConfigField "String", "BITCOIN_DEFAULT_PEER", "\"10.0.2.2\""
        buildConfigField "String", "BITCOIN_DEFAULT_PEER", "\"134.122.59.107\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
    }

    buildFeatures {
        viewBinding = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'nl.tudelft.trustchain.common'
}

repositories {
    maven { url "https://dl.bintray.com/terl/lazysodium-maven" }
}

dependencies {
    api(project(':ipv8-android')) {
        exclude group: 'net.java.dev.jna'
        exclude group: 'org.bouncycastle'
    }

    api 'com.google.guava:guava:30.0-android'

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.fragment:fragment-ktx:${fragment_version}"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // QR
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'

    // Material
    implementation 'com.google.android.material:material:1.1.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Logging
    implementation 'io.github.microutils:kotlin-logging:1.7.7'

    implementation 'com.github.MattSkala:recyclerview-itemadapter:0.4'

    implementation group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.15.10'

    api project(':geth-android')

    // BitTorrent
    implementation files('libs/jlibtorrent-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-arm64-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-arm-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-x86-' + jlibtorrent_version + '.jar')
    implementation files('libs/jlibtorrent-android-x86_64-' + jlibtorrent_version + '.jar')

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.3'
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation 'org.json:json:20190722'
    testImplementation "com.squareup.sqldelight:sqlite-driver:$sqldelight_version"
    testImplementation "com.goterl:lazysodium-java:5.1.4"
    annotationProcessor "androidx.room:room-compiler:$room_version"
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
        "-opt-in=kotlin.RequiresOptIn"
    ]
}
