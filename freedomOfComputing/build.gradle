apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

ktlint {
    version = "$ktlint_version"
    android = true
    outputToConsole = true
    ignoreFailures = false
    verbose = true
}

android {
    defaultConfig {
        minSdkVersion 26
        compileSdk 34
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
        allWarningsAsErrors = true
    }

    buildFeatures {
        viewBinding = true
    }

    namespace 'nl.tudelft.trustchain.foc'

}

dependencies {
    implementation project(':common')

    // TODO fix direct import, this should rely on common.
    // BitTorrent
    implementation files('../common/libs/jlibtorrent-' + jlibtorrent_version + '.jar')
    implementation files('../common/libs/jlibtorrent-android-arm64-' + jlibtorrent_version + '.jar')
    implementation files('../common/libs/jlibtorrent-android-arm-' + jlibtorrent_version + '.jar')
    implementation files('../common/libs/jlibtorrent-android-x86-' + jlibtorrent_version + '.jar')
    implementation files('../common/libs/jlibtorrent-android-x86_64-' + jlibtorrent_version + '.jar')

    api "androidx.appcompat:appcompat:$appcompat_version"
    api "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.core:core-ktx:$core_version"

    api "com.google.android.material:material:$material_version"
    implementation 'io.github.microutils:kotlin-logging:1.7.7'
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '3.0.0'
    androidTestImplementation "androidx.test.ext:junit:$android_junit_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Transitive dependencies
    androidTestImplementation 'androidx.test:monitor:1.6.1'
    androidTestImplementation "junit:junit:$junit_version"
    api 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    api(project(':ipv8')) {
        exclude module: 'lazysodium-java'
    }
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation 'androidx.transition:transition:1.4.1'
    implementation project(':ipv8-android')
    testImplementation "io.mockk:mockk-dsl:$mockk_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            "-opt-in=kotlin.RequiresOptIn"
    ]
}

tasks.withType(Test) {
    systemProperty "java.library.path", "../common/native_libs"
}
