apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlinx-serialization'
apply plugin: "dagger.hilt.android.plugin"
apply plugin: "kotlin-kapt"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        classpath("com.google.dagger:hilt-android-gradle-plugin:$dagger_version")

    }
}

ktlint {
    version = "$ktlint_version"
    android = true
    outputToConsole = true
    ignoreFailures = false
    verbose = true
}

// Generate a version code from git commit count
static def generateVersionCode() {
    // Unix
    def result = "git rev-list HEAD --count".execute().text.trim()
    // Windows
    if (result.empty) result = "PowerShell -Command git rev-list HEAD --count".execute().text.trim()
    if (result.empty) throw new RuntimeException("Could not generate versioncode: ${result.text}")
    println("Generated the following version code: " + result.toInteger())
    return result.toInteger()
}


android {
    defaultConfig {
        applicationId "nl.tudelft.trustchain"
        minSdkVersion 26
        compileSdk 34
        targetSdkVersion 34
        versionCode generateVersionCode()
        versionName "0.3.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias "trustchain"
            keyPassword "N/A"
            storeFile file("build.keystore")
            storePassword "N/A"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['../common/libs']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        viewBinding = true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/kotlinx-coroutines-core.kotlin_module', 'META-INF/DEPENDENCIES']
        }
    }

    namespace 'nl.tudelft.trustchain.app'
}

repositories {
    maven {
        url = 'https://raw.githubusercontent.com/blockchain/api-v1-client-java/mvn-repo/'
    }
    mavenCentral()
    jcenter()
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

dependencies {
    implementation project(':debug')
    implementation project(':freedomOfComputing')
    implementation project(':eurotoken')
    implementation project(':valuetransfer')
    implementation project(':peerai')
    implementation project(':common')
    implementation project(':currencyii')
    implementation project(':musicdao')

    // AndroidX
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:21.2.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.3.1'

    // Material
    implementation "com.google.android.material:material:$material_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Logging
    implementation('net.java.dev.jna:jna:5.12.1@aar')

    implementation "com.github.MattSkala:recyclerview-itemadapter:$recyclerview_adapter_version"
    implementation project(':currencyii')
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'

    // Testing
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$android_junit_version"

    // Hilt
    implementation("com.google.dagger:hilt-android:$dagger_version")
    kapt("com.google.dagger:hilt-android-compiler:$dagger_version")

    // Room
    implementation "androidx.room:room-runtime:$room_version"

    // Transitive dependencies
    androidTestImplementation 'androidx.test:monitor:1.6.1'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation "androidx.core:core:$core_version"
    implementation 'androidx.datastore:datastore-core:1.0.0'
    implementation 'androidx.datastore:datastore-preferences-core:1.0.0'
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.sqlite:sqlite:2.4.0'
    implementation "app.cash.sqldelight:android-driver:$sqldelight_version"
    implementation "app.cash.sqldelight:runtime:$sqldelight_version"
    implementation 'com.google.dagger:dagger:2.50'
    implementation "com.google.dagger:hilt-core:$dagger_version"
    implementation 'com.google.errorprone:error_prone_annotations:2.9.0'
    implementation 'com.google.guava:guava:31.0.1-android'
    implementation 'javax.inject:javax.inject:1'
    implementation(project(':ipv8')) {
        exclude module: 'lazysodium-java'
    }
    implementation project(':ipv8-android')
    kapt 'com.google.dagger:dagger-compiler:2.50'
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            "-opt-in=kotlin.ExperimentalUnsignedTypes",
            "-opt-in=kotlin.RequiresOptIn"
    ]
}
