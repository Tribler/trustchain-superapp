apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "kotlinx-serialization"

android {
    compileSdkVersion 31

    useLibrary "android.test.base"
    useLibrary "android.test.mock"


    viewBinding {
        enabled = true
    }

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        consumerProguardFiles "consumer-rules.pro"

        // The address of the MultiSig wallet deployed on the goerli test network.
        buildConfigField "String", "ETH_TEST_MULTISIG_ADDR", "\"0x2DfEecF7d2f5363149cC73cAb96C00575c732170\""

        // The default bitcoin/eurotoken wallet address that is filled in for the pool at startup. This address
        // is mostly used for testing and should be replaced with a real address as soon as a real pool
        // is up and running.
        buildConfigField "String", "DEFAULT_EURO_POOL_ADDR", "\"4c69624e61434c504b3a85f8c38cec5caa72c2e23da91d0288d7f49615ee400ca82e97e925e1a2f7ae33460843aea94611d04eb535f463a9857f592a7cb072eb7e0b52f080ef078c7c5f\""
        buildConfigField "String", "DEFAULT_BTC_POOL_ADDR", "\"mv7x4cqQMM8ptFgiHwJZTMTQTcTFV5rteU\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_15
        targetCompatibility JavaVersion.VERSION_15
    }
    kotlinOptions {
        jvmTarget = "15"
        allWarningsAsErrors = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/license.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/notice.txt"
        exclude "META-INF/ASL2.0"
        exclude("META-INF/*.kotlin_module")
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs '../common/libs'
        }
    }
}

dependencies {
    implementation project(":common")
    implementation project(":ipv8-android")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Android Libraries
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // Layout libraries
    implementation "com.google.android.material:material:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0"

    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0"

    implementation "com.google.android:flexbox:2.0.1"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:1.0-M1-1.4.0-rc"

    // Wallet libraries
    implementation group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.15.10'
    implementation(group: 'org.web3j', name: 'core', version: '4.6.0-android') {
        exclude group: 'org.bouncycastle'
    }
    implementation group: 'org.ethereum', name: 'geth-android-all-1.10.1-c2d2f4ed', ext: 'aar'

    // Testing
    testImplementation "junit:junit:4.12"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.3"

    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
        "-Werror", // Set Kotlin compiler warnings as errors
        "-opt-in=kotlin.RequiresOptIn"
    ]
}

tasks.withType(Test) {
    systemProperty "java.library.path", "./src/main/jniLibs"
}
